/*
 * "Chatty Imoen" for BGEE
 * AstroBryGuy
 *
*/

/* Backup folder */
BACKUP ~chattyimoen/backup~

AUTHOR ~AstroBryGuy~

/* enable all error messages; nothing suppressed. comment this out for release version */
// MODDER

VERSION ~v1.3~

README ~chattyimoen/readme.html~

ALWAYS
	INCLUDE ~chattyimoen/lib/project_macros.tpa~   /* adds macros callable by each component */

	/* STATE.IDS patching - thanks, Cam, if you read it */
	/* adds custom IsValidForPartyDialogue state */
	APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

	/* CPMVars */
	ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN
		INCLUDE ~chattyimoen/lib/g3_bg_cpmvars.tpa~
	END ELSE ACTION_IF GAME_IS ~bgee~ THEN BEGIN
		INCLUDE ~chattyimoen/lib/g3_bgee_cpmvars.tpa~
	END ELSE ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN
		INCLUDE ~chattyimoen/lib/g3_tutu_cpmvars.tpa~
	END ELSE ACTION_IF GAME_IS ~bgt~ THEN BEGIN
		INCLUDE ~chattyimoen/lib/g3_bgt_cpmvars.tpa~
	END ELSE ACTION_IF GAME_IS ~eet~ THEN BEGIN
		INCLUDE ~chattyimoen/lib/eet_cpmvars.tpa~
	END

	/* Portrait size variables */
	ACTION_IF (GAME_IS ~bgee eet~) THEN BEGIN
		OUTER_SPRINT sm "m"
		OUTER_SPRINT lg "l"
	END ELSE BEGIN
		OUTER_SPRINT sm "s"
		OUTER_SPRINT lg "m"
	END

	/* HANDLE_CHARSETS */
	ACTION_DEFINE_ARRAY fl#noconvert BEGIN setup END
	ACTION_DEFINE_ARRAY fl#reload BEGIN chattyimoen END

	LAF HANDLE_CHARSETS
    	INT_VAR
      		infer_charset = 1
    	STR_VAR
      		tra_path = EVAL ~%MOD_FOLDER%/tra~
      		noconvert_array = fl#noconvert
      		reload_array = fl#reload
  	END
END

AUTO_TRA ~chattyimoen/tra/%s~
LANGUAGE ~English~ 
	~english~ 
	~chattyimoen/tra/english/setup.tra~
	~chattyimoen/tra/english/chattyimoen.tra~
LANGUAGE ~Francais~
	~french~
	~chattyimoen/tra/french/setup.tra~
	~chattyimoen/tra/french/chattyimoen.tra~
LANGUAGE ~Deutsch~
	~german~
	~chattyimoen/tra/german/setup.tra~
	~chattyimoen/tra/german/chattyimoen.tra~
LANGUAGE ~Italiano (Sottotitoli solo; voce fuori campo in lingua inglese)~ 
	~italian~ 
	~chattyimoen/tra/italian/setup.tra~
	~chattyimoen/tra/italian/chattyimoen.tra~
LANGUAGE ~Polski~
	~polish~
	~chattyimoen/tra/polish/setup.tra~
	~chattyimoen/tra/polish/chattyimoen.tra~

BEGIN @0 DESIGNATED 0 /* Chatty Imoen: Expanded Character Sounds */
  REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt bgee eet~ @1 /* You must have BG1, TotSC, Tutu, BGT, BGEE, or EET installed. */

ACTION_IF DIRECTORY_EXISTS ~chattyimoen/sounds/%LANGUAGE%~ THEN BEGIN
        COPY ~chattyimoen/sounds/%LANGUAGE%~ ~override~
END ELSE BEGIN
	COPY ~chattyimoen/sounds/english~ ~override~
END

ACTION_FOR_EACH imoen IN "%tutu_var%imoen" "%tutu_var%imoen1" "%tutu_var%imoen2" "%tutu_var%imoen4" "%tutu_imoen6%" BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME "%imoen%.cre" BEGIN
		COPY_EXISTING ~%imoen%.cre~ ~override~
			LAUNCH_PATCH_MACRO ~source_cre_sound_cleanup~
			SAY INITIAL_MEETING @100 /* [IMOEN 15] Heya! It's me, Imoen. */
			SAY MORALE @101 /* [IMOEN 7] I am gone. */
			SAY HAPPY @102 /* [IMOEN 27] Good on you if you save the day. */
			SAY UNHAPPY_ANNOYED @103 /* [IMOEN 10] Mutton-mongerin' riffraff. */
			SAY UNHAPPY_SERIOUS @104 /* [IMOEN 17] I've done had enough of this. */
			SAY UNHAPPY_BREAKING @105 /* [IMOEN 21] Gettin' out alive, I am. 'tis nothin' for me here now. */
			SAY LEADER @106 /* [AY#IMNLD] Aww, come on. I'm not cut out for the leadership stuff. */
			SAY TIRED @107 /* [IMOEN 11] *yawn* I'm gettin' a little sleepy. */
			SAY BORED @108 /* [AY#IMNBR] What's taking you so long? */
			SAY BATTLE_CRY1 @109 /* [IMOEN 18] My blade will cut you down to size! */
			SAY BATTLE_CRY2 @110 /* [AY#IMNB2] I'll show you a fight! */
			SAY DAMAGE @111 /* [IMOEN 12]  */
			SAY DYING @112 /* [IMOEN 13]  */
			SAY HURT @113 /* [AY#IMNHR] I'm kinda wounded. */
			SAY AREA_DUNGEON @114 /* [AY#IMNDN] There could be traps down here. */
			SAY AREA_NIGHT @115 /* [IMOEN 19] 'Tis something most unnatural here, and I want no part of it. */
			SAY SELECT_COMMON1 @116 /* [IMOEN 3] Yep? */
			SAY SELECT_COMMON2 @117 /* [IMOEN 4] Whatcha want? */
			SAY SELECT_COMMON3 @118 /* [AY#IMNS3] Name it. */
			SAY SELECT_COMMON4 @119 /* [AY#IMNS4] Good to go. */
			SAY SELECT_COMMON5 @120 /* [AY#IMNS5] Somethin' up? */
			SAY SELECT_COMMON6 @121 /* [AY#IMNS6] Hurry up! */
			SAY SELECT_ACTION1 @124 /* [AY#IMNA1] Gotcha. */
			SAY SELECT_ACTION2 @125 /* [AY#IMNA2] You can count on me! */
			SAY SELECT_ACTION3 @126 /* [AY#IMNA3] No problem at all. */
			SAY SELECT_ACTION4 @127 /* [IMOEN 5] Booooorrrring. */
			SAY SELECT_ACTION5 @128 /* [IMOEN 6] Ha, ye're a queer fellow. */
			SAY SELECT_ACTION6 @129 /* [AY#IMNA6] Alright, alright! */
			SAY SELECT_ACTION7 @130 /* [IMOEN 28] I know nothin' more, so leave me to go. */
			SAY INSULT @103 /* [IMOEN 10] Mutton-mongerin' riffraff. */
			SAY COMPLIMENT1 @131 /* [AY#IMNC1] Good job! */
			SAY SPECIAL1 @132 /* [AY#IMNSP] Heya! How're things goin'? */
			SAY SPECIAL2 @133 /* [IMOEN 8] Do ya wanna tell me a story 'bout trollops an' plug tails? Please? */
			SAY SPECIAL3 @134 /* [IMOEN 30] Back home, Puffguts would always tell me a story. */
			SAY RESPONSE_TO_INSULT1 @135 /* [IMOEN 26] I care not. */
			SAY RESPONSE_TO_INSULT2 @136 /* [IMOEN 9] Ye're all buffle-headed. */
			SAY MISCELLANEOUS @137 /* [AY#IMNR1] Alright! */
			SAY RESPONSE_TO_COMPLIMENT2 @138 /* [AY#IMNR2] Right you are. */
			SAY REACT_TO_DIE_GENERAL @139 /* [IMOEN 14] Poor sod, takin' the dirt nap so soon. */
			SAY REACT_TO_DIE_SPECIFIC @139 /* [IMOEN 14] Poor sod, takin' the dirt nap so soon. */
			SAY HIDDEN_IN_SHADOWS @140 /* [IMOEN 24] This way. */
			SAY PICKED_POCKET @141 /* [AY#IMNPP] Easy as pie. */
		BUT_ONLY
	END
END

// Imoen greets party with "Good to see you Rum-dukes again!" line after being kicked out.
COMPILE EVALUATE_BUFFER ~chattyimoen/dlg/ay#imoenp.d~


BEGIN @2 /* Chatty Imoen:  Add NPC Interactions */
	REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt bgee eet~ @1 /* You must have BG1, TotSC, Tutu, BGT, BGEE, or EET installed. */
	REQUIRE_COMPONENT ~setup-chattyimoen.tp2~ ~0~ @3 // ~Chatty Imoen: Expanded Character Sounds component required~

// Imoen gets specific death reaction for Garrick (no one else does)
COPY_EXISTING ~death.2da~ ~override/death.2da~
	COUNT_2DA_COLS "cnt_col"
    COUNT_2DA_ROWS %cnt_col% "cnt_row"
	FOR ( cnt=0; cnt<"%cnt_row%"; cnt=cnt+1 ) BEGIN
    	READ_2DA_ENTRY "%cnt%" 0 "%cnt_col%" "npc_name"
       	PATCH_IF ("%npc_name%" STR_EQ "%IMOEN_DV%") BEGIN
       		SET_2DA_ENTRY "%cnt%" 9 "%cnt_col%" "1" // Garrick
       	END
    END
BUT_ONLY

COPY_EXISTING ~death.2da~ ~override/death.2da~
	LPM remove_blank_lines
BUT_ONLY

// Add Imoen to INTERACT.2DA
COPY_EXISTING ~interact.2da~ ~override~
	REPLACE_TEXTUALLY ~%IMOEN_DV%~ ~_UNUSED~
BUT_ONLY

COPY_EXISTING ~interact.2da~ ~override~
	COUNT_2DA_COLS "cnt_col"
	COUNT_2DA_ROWS %cnt_col% "cnt_row"
	SPRINT new_row ~%IMOEN_DV% s 0 s 0 c s s c s s 0 i 0 s s 0 i i i i s 0 i 0~      
	FOR ( cnt=25; cnt<"%cnt_col%"; cnt=cnt+1 ) BEGIN
    	SPRINT new_row ~%new_row% 0~
	END
	SPRINT new_col "$ $ %IMOEN_DV% "
	SET cnt_row = %cnt_row% + 1
	FOR ( cnt=0; cnt<"%cnt_row%"; cnt=cnt+1 ) BEGIN
    	SPRINT new_col ~%new_col% 0~
    END    
BUT_ONLY

APPEND "interact.2da" "%new_row%"  
APPEND_COL "interact.2da" "%new_col%"

COPY_EXISTING ~interact.2da~ ~override~
	COUNT_2DA_COLS "cnt_col"
    COUNT_2DA_ROWS %cnt_col% "cnt_row"
	SET imoen_col = %cnt_col% - 1
	FOR ( cnt=0; cnt<"%cnt_row%"; cnt=cnt+1 ) BEGIN
    	READ_2DA_ENTRY "%cnt%" 0 "%cnt_col%" "npc_name"
       	PATCH_IF ("%npc_name%" STR_EQ "KIVAN") BEGIN
    		SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "ALORA") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "MINSC") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "c"
        END
        PATCH_IF ("%npc_name%" STR_EQ "DYNAHEIR") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "c"
        END
        PATCH_IF ("%npc_name%" STR_EQ "YESLICK") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "c"
        END
        PATCH_IF ("%npc_name%" STR_EQ "CORAN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "c"
        END
        PATCH_IF ("%npc_name%" STR_EQ "AJANTIS") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "s"
    	END
    	PATCH_IF ("%npc_name%" STR_EQ "KHALID") BEGIN
			SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "c"
        END
    	PATCH_IF ("%npc_name%" STR_EQ "JAHEIRA") BEGIN
    		SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
		END
        PATCH_IF ("%npc_name%" STR_EQ "GARRICK") BEGIN
    		SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "s"
    	END
		PATCH_IF ("%npc_name%" STR_EQ "SAFANA") BEGIN
			SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "FALDORN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "i"
        END
        PATCH_IF ("%npc_name%" STR_EQ "BRANWEN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "QUAYLE") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "XAN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "SKIE") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "ELDOTH") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END
        PATCH_IF ("%npc_name%" STR_EQ "XZAR") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "s"
        END
        PATCH_IF ("%npc_name%" STR_EQ "MONTARON") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "i"
        END
        PATCH_IF ("%npc_name%" STR_EQ "TIAX") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "s"
        END
        PATCH_IF ("%npc_name%" STR_EQ "KAGAIN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "i"
    	END
        PATCH_IF ("%npc_name%" STR_EQ "SHARTEEL") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
    	END
        PATCH_IF ("%npc_name%" STR_EQ "EDWIN") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "i"
        END
        PATCH_IF ("%npc_name%" STR_EQ "VICONIA") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "0"
        END 	
        PATCH_IF ("%npc_name%" STR_EQ "NEERA") BEGIN
        	SET_2DA_ENTRY "%cnt%" "%imoen_col%" "%cnt_col%" "c"
        END 	
	END
	// If Neera is added to INTERACT.2DA, flag her for compliments from Imoen
	SET imoen_row = %cnt_row% - 1
	SET neera_col = 0
	SET idx_cols = %cnt_col%-1
	FOR ( cnt=0; cnt<"%idx_cols%"; cnt=cnt+1 ) BEGIN
		READ_2DA_ENTRY 0 "%cnt%" "%idx_cols%" "npc_name"
        PATCH_IF ("%npc_name%" STR_EQ "NEERA") BEGIN
        	SET neera_col = %cnt% + 1
        END
    END	
	PATCH_IF (neera_col > 0) BEGIN
		SET_2DA_ENTRY "%imoen_row%" "%neera_col%" "%cnt_col%" "c"
	END
BUT_ONLY

COPY_EXISTING ~interact.2da~ ~override~
	PRETTY_PRINT_2DA
BUT_ONLY

COPY_EXISTING ~interact.2da~ ~override~
	LPM remove_blank_lines
BUT_ONLY


BEGIN @5 /* Give Imoen her BG1 Portrait */
	SUBCOMPONENT @4  /* Imoen Portrait Replacements */
	REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt bgee eet~ @1 /* You must have BG1, TotSC, Tutu, BGT, BGEE, or EET installed. */

OUTER_SPRINT "imoen_port_sm" ~imoen%sm%~
OUTER_SPRINT "imoen_port_lg" ~imoen%lg%~

LAUNCH_ACTION_MACRO ~change_imoen_portrait~


BEGIN @6 /* Give Imoen her SoD Portrait */
	SUBCOMPONENT @4  /* Imoen Portrait Replacements */
	REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt bgee eet~ @1 /* You must have BG1, TotSC, Tutu, BGT, BGEE, or EET installed. */

ACTION_IF ( NOT FILE_EXISTS_IN_GAME ~bdimoenl.bmp~) THEN BEGIN
	COPY ~chattyimoen/bmp/sod~ ~override~
END

OUTER_SPRINT "imoen_port_sm" ~bdimoen%sm%~
OUTER_SPRINT "imoen_port_lg" ~bdimoen%lg%~

LAUNCH_ACTION_MACRO ~change_imoen_portrait~


BEGIN @7 /* Give Imoen her BG2 Portrait */
	SUBCOMPONENT @4  /* Imoen Portrait Replacements */
	REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt bgee eet~ @1 /* You must have BG1, TotSC, Tutu, BGT, BGEE, or EET installed. */

ACTION_IF ( NOT FILE_EXISTS_IN_GAME ~nimoenl.bmp~) THEN BEGIN
	COPY ~chattyimoen/bmp/bg2~ ~override~
END

OUTER_SPRINT "imoen_port_sm" ~nimoen%sm%~
OUTER_SPRINT "imoen_port_lg" ~nimoen%lg%~

LAUNCH_ACTION_MACRO ~change_imoen_portrait~

